plugins {
	id 'java-library'
	id 'net.researchgate.release' version '2.4.0'
	id 'io.franzbecker.gradle-lombok' version '1.10'
	//id 'org.springframework.boot' version '1.5.7.RELEASE'
}

repositories {
	maven {
		url "http://nexus.navent.biz:8081/nexus/content/groups/public"
	}
	mavenLocal()
    jcenter()
}

lombok {
	version = "1.16.18"
	sha256 = ""
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
	baseName = rootProject.name
	version =  version
}

dependencies {
	// This dependency is exported to consumers, that is to say found on their compile classpath.
	api('javax.servlet:javax.servlet-api:3.1.0')
	api('io.micrometer:micrometer-core:latest.release')
	api('com.quigley:zabbixj:3')
	api('io.micrometer:micrometer-spring-legacy:latest.release')
	api('io.micrometer:micrometer-registry-jmx:latest.release')

	// This dependency is used internally, and not exposed to consumers on their own compile classpath.
	compileOnly('com.fasterxml.jackson.core:jackson-databind:2.8.9')
	compileOnly('org.springframework.boot:spring-boot-autoconfigure:1.5.6.RELEASE')
	compileOnly("org.springframework.boot:spring-boot-starter-actuator:1.5.6.RELEASE")
	compileOnly('org.apache.commons:commons-lang3:3.5')
	
	// Use JUnit test framework
	//testImplementation 'junit:junit:4.12'
}

//https://github.com/researchgate/gradle-release
release {
	failOnUnversionedFiles = false
	scmAdapters = [net.researchgate.release.GitAdapter]
	git {
		requireBranch = ''
	}
}

ext {
	groupPOM = 'com.navent.realestate'
	artefactPOM = rootProject.name
	versionPOM = version
	versionPOM = project.hasProperty('OverridetheVersion') ? project.OverridetheVersion : versionPOM

	if(versionPOM.endsWith('-SNAPSHOT')) {
		urlRepository = "$nexusBase/repositories/snapshots/"
	} else {
		urlRepository = "$nexusBase/repositories/releases/"
	}
}

task sourceJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

apply plugin: 'maven-publish'

publish {
	// Esto hace que se llame a bootRepackage para que se genere correctamente el Jar
	dependsOn assemble
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId groupPOM
			artifactId artefactPOM
			version versionPOM

			from components.java

			artifact sourceJar {
				classifier "sources"
			}
		}
	}
	repositories {
		maven {
			credentials {
				username "$nexusUser"
				password "$nexusPass"
			}
			url urlRepository
		}
	}
}

afterReleaseBuild.dependsOn publish

