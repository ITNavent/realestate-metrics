plugins {
    id 'io.franzbecker.gradle-lombok' version '1.12'
	id 'java-library'
	id 'net.researchgate.release' version '2.6.0'
	id 'eclipse'
}

repositories {
	maven {
		url "http://nexus.navent.biz:8081/nexus/content/groups/public"
	}
	mavenLocal()
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
	baseName = rootProject.name
	version =  version
}

dependencies {
	// This dependency is exported to consumers, that is to say found on their compile classpath.
	implementation('javax.servlet:javax.servlet-api:3.1.0')
	api('com.quigley:zabbixj:3')
	api("io.micrometer:micrometer-core:$micrometerVersion")
	api("io.micrometer:micrometer-registry-jmx:$micrometerVersion")
	api("io.micrometer:micrometer-registry-prometheus:$micrometerVersion")

	api('io.opentracing.brave:brave-opentracing:0.34.1')
	api('io.zipkin.reporter2:zipkin-sender-urlconnection:2.9.0')

	implementation('com.github.vladimir-bukhtoyarov:rolling-metrics:2.0.3')

	// This dependency is used internally, and not exposed to consumers on their own compile classpath.
	implementation('com.fasterxml.jackson.core:jackson-databind:2.8.11.3')
	implementation("org.springframework.boot:spring-boot-autoconfigure:$springbootVersion")
	implementation("org.springframework.boot:spring-boot-starter-actuator:$springbootVersion")
	implementation("org.springframework.boot:spring-boot-starter-web:$springbootVersion")
	implementation('org.apache.commons:commons-lang3:3.9')
}

//https://github.com/researchgate/gradle-release
release {
	failOnUnversionedFiles = false
	scmAdapters = [net.researchgate.release.GitAdapter]
	git {
		requireBranch = ''
	}
}

ext {
	groupPOM = 'com.navent.realestate'
	artefactPOM = rootProject.name
	versionPOM = version
	versionPOM = project.hasProperty('OverridetheVersion') ? project.OverridetheVersion : versionPOM

	if(versionPOM.endsWith('-SNAPSHOT')) {
		urlRepository = "$nexusBase/repositories/snapshots/"
	} else {
		urlRepository = "$nexusBase/repositories/releases/"
	}
}

task sourceJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

apply plugin: 'maven-publish'

publish {
	// Esto hace que se llame a bootRepackage para que se genere correctamente el Jar
	dependsOn assemble
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId groupPOM
			artifactId artefactPOM
			version versionPOM

			from components.java

			artifact sourceJar {
				classifier "sources"
			}
		}
	}
	repositories {
		maven {
			credentials {
				username "$nexusUser"
				password "$nexusPass"
			}
			url urlRepository
		}
	}
}

afterReleaseBuild.dependsOn publish

